plugins {
    id 'java'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.10'
    id 'org.beryx.jlink' version '2.24.1'
    id "org.unbroken-dome.test-sets" version "4.0.0"
}

testSets{
    test
    integrationTest
}

group 'com.application'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

application {
    mainModule = 'com.application.logger'
    mainClass = 'com.application.logger.HelloApplication'
}

java{
    toolchain{
        languageVersion.set(JavaLanguageVersion.of(11))
    }
}

javafx {
    version = '11.0.2'
    modules = ['javafx.controls', 'javafx.fxml']
}

dependencies {

    // Junit5 dependencies
    testRuntimeOnly     'org.junit.jupiter:junit-jupiter-engine:5.5.1'
    testImplementation  'org.junit.jupiter:junit-jupiter-api:5.5.1'
    testCompileOnly     'org.junit.jupiter:junit-jupiter-params:5.5.1'

    // TestFX dependencies
    testCompileOnly     "org.testfx:testfx-core:4.0.16-alpha"
    testCompileOnly     'org.junit.jupiter:junit-jupiter-api:5.5.1'
    testCompileOnly     "org.testfx:testfx-junit5:4.0.16-alpha"
}

tasks.withType(Test){
    jvmArgs '--enable-preview'
    useJUnitPlatform()

    testLogging {
        events("passed", "skipped", "failed")
    }
}


jlink {
    imageZip = project.file("${buildDir}/distributions/app-${javafx.platform.classifier}.zip")
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    launcher {
        name = 'app'
    }
}

jlinkZip {
    group = 'distribution'
}

task deleteFiles(type: Delete) {
    delete fileTree('build/reports/tests') {
        include '**/*.png'
    }
}


build{
    finalizedBy(deleteFiles)
}